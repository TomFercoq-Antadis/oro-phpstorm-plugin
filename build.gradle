plugins {
    id "org.jetbrains.intellij" version "0.0.43"
    id "de.undercouch.download" version "2.1.0"
}

import de.undercouch.gradle.tasks.download.Download

group 'com.oroplatform'
version '1.0.0-alpha23'

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'groovy'

def phpPluginUrl = System.getProperty("phpPluginUrl", "https://plugins.jetbrains.com/files/6610/30133/php-163.7743.20.zip")
def ideaVersion = System.getProperty("ideaVersion", "2016.3")
def phpPluginVersion = phpPluginUrl.find(/[a-z0-9\.\-]+\.zip/).replace(".zip", "")
def javaVersion = 1.8

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

intellij {
    version ideaVersion
    type 'IU'
    pluginName 'idea-oroplatform-plugin'
    plugins = ['php', 'yaml', 'java-i18n', 'properties', 'CSS', 'JavaScriptLanguage']
    updateSinceUntilBuild false
    sameSinceUntilBuild false
    downloadSources !Boolean.valueOf(System.getenv('CI'))
    sandboxDirectory "${project.rootDir}/.idea-sandbox"
}

repositories {
    mavenCentral()
}

def phpPluginFiles(String version) {
    fileTree(dir: "${project.intellij.ideaDirectory}/plugins/$version/php/lib/", include: '*.jar')
}

afterEvaluate {
    it.tasks.findByName("compileJava").dependsOn("downloadAndUnzipPhpPlugin")
    it.intellij.intellijFiles.addAll(phpPluginFiles(phpPluginVersion).getFiles())

    project.tasks.create(name: "downloadPhpPlugin", type: Download) {
        def output = new File("${project.intellij.ideaDirectory}/plugins/${phpPluginVersion}.zip")

        src phpPluginUrl
        dest output
        overwrite false
    }

    project.tasks.create(name: "downloadAndUnzipPhpPlugin", dependsOn: "downloadPhpPlugin", type: Copy) {
        from zipTree(downloadPhpPlugin.dest)
        into "${project.intellij.ideaDirectory}/plugins/$phpPluginVersion"
    }

    dependencies {
        compile phpPluginFiles(phpPluginVersion)
    }
}
